Actividad 25 de abril 2018

Workshop object oracle


/*Crear el tipo objeto: typ_cuenta el cual hará la función de un cuenta bancaria; 
Este se podría considerar un tipo de objeto simple ya que no tiene métodos ni columnas que 
derivan de otros objetos.*/


CREATE OR REPLACE TYPE  typ_cuenta IS OBJECT
(
    account_num         NUMBER,     --NÚMERO DE CUENTA
    account_holder      VARCHAR2(40),   --TITULAR DE LA CUENTA
    balance             NUMBER,     --SALDO
    interest_rate       NUMBER      --TASA DE INTERÉS
);


/*El siguiente bloque implementa el tipo de objeto: typ_cuenta antes creado;  Notar que, para asignar 
nuevos valores a un variable de ese tipo, es necesario invocar su constructor (nombre del tipo objeto) 
pasándole valores a cada uno de sus atributos, mientras que, para extraer los valores almacenados, se 
utiliza el nombre de la variable seguido de un punto '.' más el nombre del atributo en cuestión. */


SET SERVEROUTPUT ON
DECLARE
    v_account   typ_cuenta;
BEGIN
    v_account   :=  typ_cuenta
                                          (
                                            1000245,
                                            'Julio Tiburcio Vargas',
                                            60000,
                                            0.015
                                          );
    DBMS_OUTPUT.PUT_LINE
                        (
                            'Banco La Minita, donde su dinero pesa!!!'||CHR(10)||
                            RPAD('-',40,'-')||CHR(10)||
                            'Detalles de Cuenta:'||CHR(10)||
                            'Cliente: '||v_account.account_holder||CHR(10)||
                            'Cuenta#: '||v_account.account_num||CHR(10)||
                            'Balance Disponible: '||v_account.balance||CHR(10)||
                            'Tasa de Interés: '||v_account.interest_rate*100||'% Anual'
                        );
END;


A Continuación, crear otros Tipos de Objetos que incluirán el Objeto antes creado,  para así hacerlo un tipo complejo.
/*Crear el tipo de objeto: typ_titular que hará la función de un titular de una cuenta bancaria. */


CREATE OR REPLACE TYPE  typ_titular IS OBJECT
(
    identity_id         NUMBER,     --NÚMERO DE IDENTIDAD
    first_name          VARCHAR2(20),  --NOMBRE
    last_name           VARCHAR2(20),  --APELLIDO
    sex                 CHAR(1),
    birth_date          DATE,           --FECHA DE NACIMIENTO
    address             VARCHAR2(100),  --DIRECCIÓN
    contact_number      VARCHAR2(10),   --NÚMERO DE CONTACTO
    email               VARCHAR2(60)
);


/*Ahora Re-Crear el tipo de objeto: typ_cuenta con el atributo:  account_holder del tipo de dato: typ_titular (Object Type). 
Nota: si se presenta la necesidad de modificar /dropear el tipo de objeto: typ_titular es requerido hacer DROP del tipo: 
typ_cuenta, ya que este último es una dependencia del primero. */


CREATE OR REPLACE TYPE  typ_cuenta IS OBJECT
(
    account_num         NUMBER,     --NÚMERO DE CUENTA
    account_holder      typ_titular,   --TITULAR DE LA CUENTA
    balance             NUMBER,     --SALDO
    interest_rate       NUMBER      --TASA DE INTERÉS
);


/*El siguiente ejemplo usa el tipo de objeto: typ_cuenta el cual contiene un atributo(account_holder) de tipo objeto: 
typ_titular. 

Notar que se crearon dos variables tipo objeto: typ_titular y typ_cuenta   respectivamente, para así poder asignar 
valores al atributo en tipo objeto. 

El modo de extracción de los datos de un atributo tipo objeto seria como se muestra en el ejemplo: 
nombre_variable.nombre_atributo.nombre_atributo.*/


SET SERVEROUTPUT ON
DECLARE
    v_holder    typ_titular;
    v_account  typ_cuenta;
BEGIN
    v_holder    :=  typ_titular
                                            (
                                                '01201087659',
                                                'Julio',
                                                'Tiburcio Vargas',
                                                'M',
                                                TO_DATE('25/02/1984','DD/MM/YYYY'),
                                                'Calle la Palma #3, El Higuero, San Cristóbal, RD.',
                                                '8090002277',
                                                'jtibur_v@magicplsql.com'
                                            );

    v_account   :=  typ_cuenta
                                          (
                                            1000245,
                                            v_holder,
                                            60000,
                                            0.015
                                          );
    DBMS_OUTPUT.PUT_LINE
                        (
                            'Banco La Minita, donde su dinero pesa!!!'||CHR(10)||
                            RPAD('=',40,'=')||CHR(10)||
                            'Datos del Titular:'||CHR(10)||
                            'Nombre: '||v_account.account_holder.first_name||' '||v_account.account_holder.last_name||CHR(10)||
                            'Sexo: '||v_account.account_holder.sex||CHR(10)||
                            'Edad: '||     --Edad = A los meses entre birth_date y hoy dividido por 12
        TRUNC(
          MONTHS_BETWEEN(
           SYSDATE, v_account.account_holder.birth_date)/12
          )||CHR(10)
                        );

    DBMS_OUTPUT.PUT_LINE
                        (
                            RPAD('-',40,'-')||CHR(10)||
                            'Detalles de Cuenta:'||CHR(10)||
                            'Cuenta#: '||v_account.account_num||CHR(10)||
                            'Balance Disponible: '||v_account.balance||CHR(10)||
                            'Tasa de Interés: '||v_account.interest_rate*100||'% Anual'
                        );
END;


/*Crear el Tipo: typ_detalle_cuenta el cual representara los detalles de una cuenta bancaria. */


CREATE OR REPLACE TYPE typ_detalle_cuenta IS OBJECT
(
    account_number      NUMBER,         --NÚMERO DE CUENTA
    account_title       VARCHAR2(30),   --TITULO DE LA CUENTA
    account_type        CHAR(1),         --TIPO DE CUENTA
    open_date           DATE,       --FECHA APERTURA
    balance             NUMBER,     --SALDO
    last_tranc          CHAR(1),     --ÚLTIMA TRANSACCIÓN
    last_tranc_date     DATE,       --FECHA ÚLTIMA TRANSACCIÓN
    interest_rate       NUMBER      --TASA DE INTERÉS
);


/*Crear un tipo tabla: t_detalle_cuenta que contendrá registros del tipo:  typ_detalle_cuenta antes creado. */

CREATE OR REPLACE TYPE t_detalle_cuenta
        IS TABLE OF typ_detalle_cuenta;


/* re-crear el tipo: typ_cuenta para que contenga dos campos referentes a los tipos antes creados (typ_titular 
y t_detalle_cuenta) */


CREATE OR REPLACE TYPE  typ_cuenta IS OBJECT
(
    account_holder      typ_titular,   --TITULAR DE LA CUENTA
    account                 t_detalle_cuenta     --CUENTA
);


/* En el siguiente ejemplo para poder asignar valores a una variable de tipo objeto tabla es necesario también 
invocar su constructor sin parámetros (esto porque no tiene atributos), luego es necesario invocar su método EXTEND 
que agrega un registro en la tabla, posteriormente para asignar los valores a cada atributo del registro, es necesario 
invocar su constructor como tal. 

Notar que es necesario indicar el registro de la variable tipo tabla al cual se desea agregar los datos: variable(índice) :=. 
El medio de extracción es igual: variable(índice). nombre_atributo. */


SET SERVEROUTPUT ON
DECLARE
    v_account       typ_cuenta;
    v_holder         typ_titular;
    v_acc_detail    t_detalle_cuenta;

    v_acc_type      CHAR(1);
    v_open_date     DATE;
    v_int_rate      NUMBER(3,3);
BEGIN
    v_holder    := typ_titular
                                            (
                                                '01201087659',
                                                'Julio',
                                                'Tiburcio Vargas',
                                                'M',
                                                TO_DATE('25/02/1984','DD/MM/YYYY'),
                                                'Calle la Palma #3, El Higuero, San Cristóbal, RD.',
                                                '8090002277',
                                                'jtibur_v@magicplsql.com'
                                            );


    v_acc_detail    :=  t_detalle_cuenta();  --Primero se inicializa el constructor para poder asignar datos al tipo tabla.






    FOR i IN 1..3 LOOP
        CASE
            WHEN i<2 THEN
                v_acc_type  :=  'D';       --DÉBITO
                v_open_date :=  TO_DATE('01/04/2002','DD/MM/YYYY');
                v_int_rate  :=  .015;
            WHEN i>2 THEN
                v_acc_type  :=  'P';       --PREPAGO
                v_open_date :=  TO_DATE('09/09/2012','DD/MM/YYYY');
                v_int_rate  :=  .16;
            ELSE
                v_acc_type  :=  'C';       --CRÉDITO
                v_open_date :=  TO_DATE('07/07/2005','DD/MM/YYYY');
                v_int_rate  :=  .06;
        END CASE;

        v_acc_detail.EXTEND;     --Agrega un nuevo registro a la Tabla.
        v_acc_detail(i) :=  typ_detalle_cuenta
                                                     (
                                                        1000244+i,
                                                        'Cuenta '||i,
                                                        v_acc_type,
                                                        v_open_date,
                                                        TRUNC(DBMS_RANDOM.value(60000,150000),2),
                                                        'R',    --Retiro
                                                        SYSDATE - i,
                                                        v_int_rate
                                                     );
    END LOOP;

    v_account   :=  typ_cuenta
                                          (
                                            v_holder,
                                            v_acc_detail
                                          );



     --Para extraer los datos:
    DBMS_OUTPUT.PUT_LINE
                       (
                            'Cliente: '||v_account.account_holder.first_name||' '||v_account.account_holder.last_name||CHR(10)||
                            RPAD('=',35,'=')||CHR(10)||
                            'Cuentas: '||CHR(10)||
                            RPAD('-',35,'-')||CHR(10)
                       );

    FOR i IN v_account.account.FIRST..v_account.account.LAST LOOP    --Del primero al Último
        DBMS_OUTPUT.PUT_LINE
                           (
                                'Número : '||v_account.account(i).account_number||CHR(10)||
                                'Tipo : '||v_account.account(i).account_type||CHR(10)||
                                'Fecha Apertura : '||v_account.account(i).open_date||CHR(10)||
                                'Balance : '||v_account.account(i).balance||CHR(10)||
                                'Tasa de Interés : '||v_account.account(i).interest_rate*100||'% Anual'||CHR(10)||
                                '...'
                           );
    END LOOP;
END;


